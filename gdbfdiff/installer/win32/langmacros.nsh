;;
;; Windows GDBFDIFF NSIS installer language macros
;;

!macro GDBFDIFF_MACRO_DEFAULT_STRING LABEL VALUE
  !ifndef "${LABEL}"
    !define "${LABEL}" "${VALUE}"
    !ifdef INSERT_DEFAULT
      !warning "${LANG} lang file mising ${LABEL}, using default.."
    !endif
  !endif
!macroend

!macro GDBFDIFF_MACRO_LANGSTRING_INSERT LABEL LANG
  LangString "${LABEL}" "${LANG_${LANG}}" "${${LABEL}}"
  !undef "${LABEL}"
!macroend

!macro GDBFDIFF_MACRO_LANGUAGEFILE_BEGIN LANG
  !define CUR_LANG "${LANG}"
!macroend

!macro GDBFDIFF_MACRO_LANGUAGEFILE_END
  !define INSERT_DEFAULT
  !include "${GDBFDIFF_DEFAULT_LANGFILE}"
  !undef INSERT_DEFAULT

  ; GDBFDIFF Language file Version 2
  ; String labels should match those from the default language file.
  
  ; .onInit checks
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_NO_GTK				${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_NO_INSTALL_OVER			${CUR_LANG}

  ; License Page
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_LICENSE_BUTTON			${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_LICENSE_BOTTOM_TEXT		${CUR_LANG}

  ; Components Page
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_SECTION_TITLE			${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_SECTION_DESCRIPTION		${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT TRANSLATIONS_SECTION_TITLE		${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT TRANSLATIONS_SECTION_DESCRIPTION	${CUR_LANG}
  
  ; Installer Finish Page
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_FINISH_VISIT_WEB_SITE		${CUR_LANG}

  ; GDBFDIFF Section Prompts and Texts
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_UNINSTALL_DESC			${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_PROMPT_WIPEOUT			${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT GDBFDIFF_PROMPT_DIR_EXISTS		${CUR_LANG}

  ; Uninstall Section Prompts
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT un.GDBFDIFF_UNINSTALL_ERROR_1		${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT un.GDBFDIFF_UNINSTALL_ERROR_2		${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT un.GDBFDIFF_UNINSTALLATION_WARNING	${CUR_LANG}
  !insertmacro GDBFDIFF_MACRO_LANGSTRING_INSERT un.GDBFDIFF_DOTGDBFDIFF_WARNING		${CUR_LANG}

  !undef CUR_LANG
!macroend

!macro GDBFDIFF_MACRO_INCLUDE_LANGFILE LANG FILE
  !insertmacro GDBFDIFF_MACRO_LANGUAGEFILE_BEGIN "${LANG}"
  !include "${FILE}"
  !insertmacro GDBFDIFF_MACRO_LANGUAGEFILE_END
!macroend
